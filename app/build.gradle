apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' //Dager2
apply plugin: 'android-apt' //ButterKnife
apply plugin: 'me.tatarka.retrolambda'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    compileOptions.incremental = false //Remove and see if Dagger still bugs

    defaultConfig {
        applicationId "com.cleytongoncalves.centralufmt"
        targetSdkVersion 25
        minSdkVersion 15
        versionCode 2000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.2.0'
        resConfigs "pt"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            versionNameSuffix " Debug"
            debuggable true
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SHARED_PREFERENCES_KEY', SharedPreferencesKey
        it.resValue 'string', 'MAPS_API_KEY', MapsApiKey
        it.resValue 'string', 'BUGSNAG_API_KEY', BugSnagApiKey

        it.buildConfigField 'String', 'DEFAULT_LOGIN_RGA', LoginRga //TODO: Remove on release
        it.buildConfigField 'String', 'DEFAULT_LOGIN_PASS', LoginPassword //TODO: Remove on release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '25.0.1'
    final BUTTERKNIFE_VERSION = '8.4.0'
    final EVENTBUS_VERSION = '3.0.0'
    final OKHTTP_VERSION = '3.4.1'
    final JSOUP_VERSION = '1.10.1'
    final DAGGER_VERSION = '2.7'
    final GSON_VERSION = '2.7'
    final GMAPS_VERSION = '10.0.1'
    final GMAPS_UTILS_VERSION = '0.4.4'
    final JODA_TIME_VERSION = '2.9.4.3'
    final AUTOFITTEXTVIEW_VERSION = '0.2.1'
    final TIMBER_VERSION = '4.3.1'
    final LEAKCANARY_VERSION = '1.5'
    final BUGSNAG_VERSION = '3.4.0'
    final IMMUTABLES_VERSION = '2.3.9'
    final JOBQUEUE_VERSION = '2.0.1'

    //compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:gridlayout-v7:$SUPPORT_LIBRARY_VERSION"

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "org.greenrobot:eventbus:$EVENTBUS_VERSION"
    compile "org.jsoup:jsoup:$JSOUP_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    compile "com.google.code.gson:gson:$GSON_VERSION"

    compile "com.google.android.gms:play-services-maps:$GMAPS_VERSION"
    compile "com.google.maps.android:android-maps-utils:$GMAPS_UTILS_VERSION"

    compile "net.danlew:android.joda:$JODA_TIME_VERSION"

    compile "me.grantland:autofittextview:$AUTOFITTEXTVIEW_VERSION"

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"

    compile "com.bugsnag:bugsnag-android:$BUGSNAG_VERSION"

    apt "org.immutables:value:$IMMUTABLES_VERSION"
    provided "org.immutables:value:$IMMUTABLES_VERSION:annotations"
    provided "org.immutables:builder:$IMMUTABLES_VERSION"
    provided "org.immutables:gson:$IMMUTABLES_VERSION:annotations"

    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"

    compile "com.birbit:android-priority-jobqueue:$JOBQUEUE_VERSION"
}